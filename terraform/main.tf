resource "aws_s3_bucket" "resume_storage" {
  bucket = var.backend_bucket_name
}

resource "aws_s3_bucket_public_access_block" "resume_storage" {
  bucket = aws_s3_bucket.resume_storage.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}


## S3 bucket for frontend hosting

resource "aws_s3_bucket" "frontend" {
  bucket = var.frontend_bucket_name

}

resource "aws_s3_bucket_public_access_block" "frontend" {
  bucket = aws_s3_bucket.frontend.id

    block_public_acls       = false
    block_public_policy     = false
    ignore_public_acls      = false
    restrict_public_buckets = false
}
## S3 Bucket Website Configuration for frontend

resource "aws_s3_bucket_website_configuration" "frontend" {
  bucket = aws_s3_bucket.frontend.id

  index_document {
    suffix = "index.html"
  }
}

resource "aws_s3_bucket_policy" "frontend" {
  bucket = aws_s3_bucket.frontend.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Effect    = "Allow"
      Principal = "*"
      Action    = "s3:GetObject"
      Resource  = "${aws_s3_bucket.frontend.arn}/*"
    }]
  })
}

## DynamoDB Table for tracking resume uploads
resource "aws_dynamodb_table" "resume_uploads" {
  name         = var.dynamodb_table_name
  billing_mode = "PAY_PER_REQUEST"
  hash_key     = "resumeId"

  attribute {
    name = "resumeId"
    type = "S"
  }
}

## SNS Topic for upload alerts

resource "aws_sns_topic" "alerts" {
  name = var.sns_topic_name
}


resource "aws_iam_role" "lambda_role" {
  name = "${var.project_name}-lambda-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Effect    = "Allow"
      Principal = { Service = "lambda.amazonaws.com" }
      Action    = "sts:AssumeRole"
    }]
  })
}

## IAM Role Policy for Lambda Function

resource "aws_iam_role_policy" "lambda_policy" {
  role = aws_iam_role.lambda_role.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect   = "Allow"
        Action   = "s3:PutObject"
        Resource = "${aws_s3_bucket.resume_storage.arn}/*"
      },
      {
        Effect   = "Allow"
        Action   = "dynamodb:PutItem"
        Resource = aws_dynamodb_table.resume_uploads.arn
      },
      {
        Effect   = "Allow"
        Action   = "sns:Publish"
        Resource = aws_sns_topic.alerts.arn
      },
      {
        Effect = "Allow"
        Action = [
          "logs:CreateLogGroup",
          "logs:CreateLogStream",
          "logs:PutLogEvents"
        ]
        Resource = "*"
      }
    ]
  })
}

## Lambda Function for handling resume uploads
resource "aws_lambda_function" "upload" {
  function_name = var.lambda_function_name
  runtime       = "python3.11"
  handler       = "lambda_function.lambda_handler"
  role          = aws_iam_role.lambda_role.arn
  filename      = "lambda.zip"

  environment {
    variables = {
      UPLOAD_BUCKET = aws_s3_bucket.resume_storage.bucket
      DDB_TABLE     = aws_dynamodb_table.resume_uploads.name
      SNS_TOPIC_ARN = aws_sns_topic.alerts.arn
    }
  }
  ## lifecycle to force new deployment when code changes
  lifecycle {
    ignore_changes = [
      filename,
      source_code_hash
    ]
  }
}


## Lambda function url for API Gateway integration

resource "aws_lambda_function_url" "upload" {
  function_name      = aws_lambda_function.upload.function_name
  authorization_type = "NONE"

  cors {
    allow_origins = ["https://${aws_s3_bucket_website_configuration.frontend.website_endpoint}"]
    allow_methods = ["POST"]
    allow_headers = ["*"]
  }
}


## genrate config.js for frontend with Lambda URL

resource "local_file" "frontend_config" {
  filename = "${path.module}/../frontend/config.js"

  content = <<EOF
// Generated by Terraform
const LAMBDA_UPLOAD_URL = "${aws_lambda_function_url.upload.function_url}";
EOF
}

# ## Upload frontend files to S3 bucket

# resource "aws_s3_object" "frontend_files" {
#   depends_on = [local_file.frontend_config]

#   for_each = fileset("${path.module}/../frontend", "**")

#   bucket = aws_s3_bucket.frontend.bucket
#   key    = each.value
#   source = "${path.module}/../frontend/${each.value}"

#   content_type = lookup(
#     {
#       html = "text/html"
#       js   = "application/javascript"
#     },
#     split(".", each.value)[length(split(".", each.value)) - 1],
#     "binary/octet-stream"
#   )
# }

resource "aws_s3_object" "frontend_index" {
  bucket       = aws_s3_bucket.frontend.bucket
  key          = "index.html"
  source       = "${path.module}/../frontend/index.html"
  content_type = "text/html"
}

resource "aws_s3_object" "frontend_config" {
  depends_on = [local_file.frontend_config]

  bucket       = aws_s3_bucket.frontend.bucket
  key          = "config.js"
  source       = "${path.module}/../frontend/config.js"
  content_type = "application/javascript"
}
